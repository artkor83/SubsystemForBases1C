#использовать "."

Перем Форма;
Перем НастройкиБД;

Процедура Инициализация()

    //# Загружаем внешнюю компоненту
    ПодключитьВнешнююКомпоненту(ОбъединитьПути(КаталогПрограммы(), "oscript-simple-gui.dll"));
    
    УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
    Форма = УправляемыйИнтерфейс.СоздатьФорму();
    
    //# Устанавливаем обработку события ПриОткрытии
    Форма.УстановитьДействие(ЭтотОбъект, "ПриОткрытии", "ПриОткрытииФормы");
    Форма.Показать();

КонецПроцедуры


Функция ПрочитатьНастройкиБД()

	//получаем настройки подключения БД и прочие параметры
	НастройкиБД = НаборФункций.НастройкиИзФайлаJSON("./fixtures/DBSettings.json");

	Если НастройкиБД["БД"].Количество() = 0 Тогда
		Сообщить("Не задан список база данных для обновления");
		Возврат 1;
	КонецЕсли;

	Возврат 0
КонецФункции

//# Обработка события первого открытия формы
Процедура ПриОткрытииФормы() Экспорт

    Кнопка1 = Форма.Элементы.Добавить("КнопкаВыполнить", "КнопкаФормы", Неопределено);
    Кнопка1.Заголовок = "Обновить базы данных";
    Кнопка1.УстановитьДействие(ЭтотОбъект, "Нажатие", "ПриНажатииНаКнопкуВыполнить");

	ПолеВвода1 = Форма.Элементы.Добавить("НеОбновлятьБД", "ПолеФормы", Неопределено);
	ПолеВвода1.Вид = Форма.ВидПоляФормы.ПолеФлажка;
	ПолеВвода1.Заголовок = "Только сравнить-объединить";
	ПолеВвода1.Значение = истина;

	СтатусВозврата = ПрочитатьНастройкиБД();
	Если СтатусВозврата = 1 Тогда
		Форма.Закрыть();
		Возврат
	КонецЕсли;

	Группа1 =  Форма.Элементы.Добавить("ГруппаСписокБД", "ГруппаФормы", Неопределено);
	Группа1.Вид = Форма.ВидГруппыФормы.ОбычнаяГруппа;
	Группа1.Заголовок = "Список БД";

	// Цикл по базам
	Для Каждого ДанныеПодключения_Копии ИЗ НастройкиБД["БД"] Цикл

		ИмяБД = ДанныеПодключения_Копии["Имя"];
		ПолеВвода1 = Форма.Элементы.Добавить("ПолеФлажка_" + ИмяБД, "ПолеФормы", Группа1);
		ПолеВвода1.Вид = Форма.ВидПоляФормы.ПолеФлажка;
		ПолеВвода1.Заголовок = ИмяБД;
		ПолеВвода1.Значение = истина;
	
	КонецЦикла;

КонецПроцедуры

Процедура ПриНажатииНаКнопкуВыполнить() Экспорт

	Группа1 =  Форма.Элементы.Найти("ГруппаСписокБД");

	// Проверка наличия хотя бы 1 выбранной базы
	ЕстьВыбранныеБазы = ложь;
	Для Каждого ДанныеПодключения_Копии ИЗ НастройкиБД["БД"] Цикл

		ИмяБД = ДанныеПодключения_Копии["Имя"];
		Если Группа1.Элементы.Найти("ПолеФлажка_" + ИмяБД).Значение Тогда
			ЕстьВыбранныеБазы = истина;
			Прервать
		КонецЕсли

	КонецЦикла;

	Если НЕ ЕстьВыбранныеБазы Тогда
		Сообщить("Не выбрана ни 1 база для объединения");
		Возврат
	КонецЕсли;
	
	КаталогИсходников = "./out/";

	//выгружаем текущую конфигурацию из головной базы
	ДанныеПодключения_Глав = НастройкиБД.ГлавнаяБД;
	ИмяФайла_Глав		   = НастройкиБД.ИмяФайлаКонф;
	НаборФункций.ВыгрузитьКонфигурациюВКаталог(ДанныеПодключения_Глав, КаталогИсходников, ИмяФайла_Глав);

	НеОбновлятьБД = Форма.Элементы.Найти("НеОбновлятьБД").Значение;

	// Цикл по базам: сравнение-объединение и обновление конфигурации БД
	Для Каждого ДанныеПодключения_Копии ИЗ НастройкиБД["БД"] Цикл

		ИмяБД = ДанныеПодключения_Копии["Имя"];

		Если Группа1.Элементы.Найти("ПолеФлажка_" + ИмяБД).Значение Тогда
			НаборФункций.СравнитьОбъединитьКонфигурацию(ДанныеПодключения_Копии, КаталогИсходников, ИмяФайла_Глав, 
				НастройкиБД["ИмяФайлаСравненияОбъединения"], НеОбновлятьБД)
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры


Инициализация();