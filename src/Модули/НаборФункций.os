#Использовать logos
#Использовать fs
#Использовать vanessa-runner
#использовать json

Функция НастройкиИзФайлаJSON(ИмяФайла) Экспорт

	ТекстовыйДокумент = новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ИмяФайла);

	СтрокаJSON = ТекстовыйДокумент.ПолучитьТекст();
	ПарсерJSON = Новый ПарсерJSON();

	Значение = ПарсерJSON.ПрочитатьJSON(СтрокаJSON,,,Истина);

	Возврат Значение
КонецФункции

Процедура ВыгрузитьКонфигурациюВКаталог(ДанныеПодключения, ПутьКаталога, ИмяФайла) Экспорт

	ФС.ОбеспечитьПустойКаталог(ПутьКаталога);

	Команда = Новый Команда;

	ИмяЛога = Команда.ИмяЛога();
	Лог = Логирование.ПолучитьЛог(ИмяЛога);
	Лог.УстановитьУровень(0);
	
	СтрокаЗапуска = "runner unload " + 
		" " + ПутьКаталога + "/" + ИмяФайла +
		" --ibconnection "+	ДанныеПодключения["СтрокаПодключения"];
	ДополнитьЛогинПарольБД(СтрокаЗапуска, ДанныеПодключения);

	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	КодВозврата = Команда.Исполнить();
	
	Если КодВозврата = 1 Тогда
		ВызватьИсключение "Не удалось выгрузить базу "
	КонецЕсли;

КонецПроцедуры

Процедура СравнитьОбъединитьКонфигурацию(ДанныеПодключения, ПутьКаталога, ИмяФайла, НастройкиОбъединения, НеОбновлятьБД) Экспорт

	Команда = Новый Команда;

	ИмяЛога = Команда.ИмяЛога();
	Лог = Логирование.ПолучитьЛог(ИмяЛога);
	Лог.УстановитьУровень(0);
	
	// Сравниваем/объединяем...
	СтрокаЗапуска = "runner merge " + 
		" --src " + ПутьКаталога + "/" + ИмяФайла + 
		" --merge-settings " + НастройкиОбъединения +
		" --ibconnection "+	ДанныеПодключения["СтрокаПодключения"];
	ДополнитьЛогинПарольБД(СтрокаЗапуска, ДанныеПодключения);
	СтрокаЗапуска = СтрокаЗапуска + " --force";	
			
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);	
	
	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 1 Тогда
		ВызватьИсключение "Не удалось загрузить конфигурацию в базу " + ДанныеПодключения["СтрокаПодключения"]
	КонецЕсли;

	// Обновляем конфигурацию БД
	Если НЕ НеОбновлятьБД Тогда
		ОбновитьКонфигурациюБД(ДанныеПодключения);
	КонецЕсли
	
КонецПроцедуры

Процедура ОбновитьКонфигурациюБД(ДанныеПодключения) 

	ЛогКонфигуратора = Логирование.ПолучитьЛог("oscript.lib.v8runner");
	ЛогКонфигуратора.УстановитьУровень(0);

	МенеджерКонфигуратора = Новый МенеджерКонфигуратора;

	МенеджерКонфигуратора.Инициализация( ДанныеПодключения.СтрокаПодключения,
		ДанныеПодключения.Пользователь,
		ДанныеПодключения.Пароль);

	Попытка
			УправлениеКонфигуратором = МенеджерКонфигуратора.УправлениеКонфигуратором();
			УправлениеКонфигуратором.ОбновитьКонфигурациюБазыДанных(,,истина); 
			ЛогКонфигуратора.Информация(УправлениеКонфигуратором.ВыводКоманды());
			ЛогКонфигуратора.Информация("Успешно завершено Обновление БД");
	Исключение
		МенеджерКонфигуратора.Деструктор();
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

	МенеджерКонфигуратора.Деструктор();

КонецПроцедуры


Процедура ДополнитьЛогинПарольБД(СтрокаЗапуска, ДанныеПодключения)

	Если Не ПустаяСтрока(ДанныеПодключения["Пользователь"]) Тогда
		СтрокаЗапуска = СтрокаЗапуска + " --db-user " 	  + ДанныеПодключения["Пользователь"];
	КонецЕсли;	
	Если Не ПустаяСтрока(ДанныеПодключения["Пароль"]) Тогда
		СтрокаЗапуска = СтрокаЗапуска + " --db-pwd " 	  + ДанныеПодключения["Пароль"];	
	КонецЕсли;

КонецПроцедуры

